apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jakewharton.butterknife'
group = 'com.github.DragonsQC'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    compileOptions.encoding = 'UTF-8'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
//【Android Support】*******************************************************************************
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:percent:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'
    compile 'com.android.support:palette-v7:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
    //compile 'com.android.support:design:23.1.1'

//【HTTP】******************************************************************************************
    //xUtils [https://github.com/wyouflf/xUtils3]
    compile 'org.xutils:xutils:3.3.36'
    //Retrofit2 [https://github.com/square/retrofit]
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    //converter-gson [http://www.mvnrepository.com/artifact/com.squareup.retrofit2/converter-gson]
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    //logging-interceptor [http://www.mvnrepository.com/artifact/com.squareup.okhttp3/logging-interceptor]
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    //fastjson [http://www.mvnrepository.com/artifact/com.alibaba/fastjson]
    compile 'com.alibaba:fastjson:1.1.52.android'

//【图片加载】***************************************************************************************
    //Picasso [https://github.com/square/picasso]
    compile 'com.squareup.picasso:picasso:2.5.2'
    //Glide [https://github.com/bumptech/glide]
    compile 'com.github.bumptech.glide:glide:3.7.0'

//【Utils】
    //logger [https://github.com/orhanobut/logger]
    compile 'com.orhanobut:logger:1.13'
    //UCrop [https://github.com/Yalantis/uCrop]
    compile 'com.yalantis:ucrop:2.1.1'

//【Animation】*************************************************************************************
    //NineoldAndroids [https://github.com/JakeWharton/NineOldAndroids]
    compile 'com.nineoldandroids:library:2.4.0'
    //"YoYo" 动画效果 AndroidViewAnimations [https://github.com/daimajia/AndroidViewAnimations]
    compile 'com.daimajia.easing:library:1.0.1@aar'
    compile 'com.daimajia.androidanimations:library:1.1.3@aar'

//【Widget】******************************************************************************************
    //Bootstrap [https://github.com/Bearded-Hen/Android-Bootstrap]
    compile 'com.beardedhen:androidbootstrap:2.3.0'
    //KenBurnsView [https://github.com/flavioarfaria/KenBurnsView]
    compile 'com.flaviofaria:kenburnsview:1.0.7'
    //ListView、GridView、RecyclerView 载入特效 [https://github.com/twotoasters/JazzyListView]
    compile 'com.twotoasters.jazzylistview:library:1.2.1'
    compile 'com.twotoasters.jazzylistview:library-recyclerview:1.2.1'
    //Material Dialogs [https://github.com/afollestad/material-dialogs]
    compile 'com.afollestad.material-dialogs:core:0.8.6.2'
    //Material Design Library [https://github.com/rey5137/Material]
    compile 'com.github.rey5137:material:1.2.4'
    //SuperToasts [https://github.com/JohnPersano/SuperToasts]
    compile 'com.github.johnpersano:supertoasts:2.0'
    //【SnappingStepper】Android-SnappingStepper [https://github.com/saiwu-bigkoo/Android-SnappingStepper]
    compile 'com.bigkoo:snappingstepper:1.0.2'
    //【轮播图】[https://github.com/daimajia/AndroidImageSlider]
    compile 'com.daimajia.slider:library:1.1.5@aar'
    //【拖拽排序】Advanced RecyclerView [https://github.com/h6ah4i/android-advancedrecyclerview]
    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.9.1@aar') {
        transitive = true
    }
    //【实时监听 ScrollView 滚动】ObservableScrollView [https://github.com/ksoichiro/Android-ObservableScrollView]
    compile 'com.github.ksoichiro:android-observablescrollview:1.6.0'
    //【Badge】BGABadgeView-Android [https://github.com/bingoogolapple/BGABadgeView-Android]
    compile 'cn.bingoogolapple:bga-badgeview:1.1.2@aar'

}
repositories {
    jcenter()
    //mavenCentral()
    maven { url "https://jitpack.io" }
    //maven { url 'http://maven.oschina.net/content/groups/public' }
    //maven { url 'https://jcenter.bintray.com' }
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}